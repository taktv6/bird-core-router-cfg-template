################################################################################
#                                Routing Policy                                #
################################################################################

define AS_LOCAL = 12345;
define AGGREGATE_ROUTE = (<aggregate route community>);
define DUESSELDORF_ROUTE = (<duesseldorf route community>);
define FRANKFURT_ROUTE = (<frankfurt route community>);
define BERLIN_ROUTE = (<berlin route community>);
define GLOBAL_ROUTE = (<global route community>);
define METRO_ROUTE = (<metro route community>);
define ORIGINATE = (<originate route community>);

function community_in() {
	if DRAINED = 1 then
		return 0;
	bgp_local_pref = 10000;
	bgp_med = 0;
}

filter drain {
	reject;
}

filter accept_all {
        accept;
}

filter ibgp_in {
	if (GLOBAL_ROUTE ~ bgp_community) then {
		accept;
	}
	if (METRO_ROUTE ~ bgp_community) then {
		if (LOCAL_METRO ~ bgp_community) then {
			accept;
		}
		else {
			bgp_local_pref = 5;
			accept;
		}
	}
	accept;
}

function peer_ip_filter_in() {
	if DRAINED = 1 then
		return 0;
	if net ~ [
                10.0.0.0/8{8,32},
                172.16.0.0/12{8,32},
                192.168.0.0/16{16,32},
                169.254.0.0/16{16,32},
                224.0.0.0/3{3,32},
                185.66.192.0/22{22,32},
                100.64.0.0/10{10,32},
                0.0.0.0/8{8,32},
                127.0.0.0/8{8,32},
                192.0.0.0/24{24,32},
                198.18.0.0/15{15,32},
                198.51.100.0/24{24,32},
                203.0.113.0/24{24,32},
                240.0.0.0/4{4,32},
                255.255.255.255/32,
                0.0.0.0/0,
		#
		# IXP LANs
		80.81.192.0/21{21,32}
                ] then {
                return 0;
        }
        if net ~ [ 0.0.0.0/0{8,24} ] then {
                return 1;
        }
        return 0;
}

function peer_ip_filter_out() {
	if net ~ [
		<prefixes to announce>
	] then {
		return 1;
	}
	return 0;
}

function set_originated_routes_as_path()
{
	if originate_aggregate_route() = 1 then {
		bgp_path.empty;
		bgp_path.prepend(AS_LOCAL);
	}
	if originate_metro_route() = 1 then {
		bgp_path.empty;
		bgp_path.prepend(AS_LOCAL);
	}
}

function tag_originated_routes()
{
	if originate_aggregate_route() = 1 then {
		bgp_community.add(AGGREGATE_ROUTE);
		bgp_community.add(LOCAL_METRO);
		bgp_community.add(ORIGINATE);
	}
	if originate_metro_route() = 1 then {
		bgp_community.add(METRO_ROUTE);
                bgp_community.add(LOCAL_METRO);
		bgp_community.add(ORIGINATE);
	}
}

filter ibgp_out {
	tag_originated_routes();
	accept;
}

function ebgp_out(string rt_scope)
int path_prepend;
{
	if DRAINED = 1 then {
                return 0;
	}

	if ("bilateral-metro-transit" = rt_scope) || ("metro-transit-customer" = rt_scope) then {
		if net ~ [
		    10.0.0.0/8{8,32},
		    172.16.0.0/12{8,32},
		    192.168.0.0/16{16,32},
		    169.254.0.0/16{16,32},
		    224.0.0.0/3{3,32},
		    100.64.0.0/10{10,32},
		    0.0.0.0/8{8,32},
		    127.0.0.0/8{8,32},
		    192.0.0.0/24{24,32},
		    198.18.0.0/15{15,32},
		    198.51.100.0/24{24,32},
		    203.0.113.0/24{24,32},
		    240.0.0.0/4{4,32},
		    255.255.255.255/32,
		    0.0.0.0/0
		    ] then {
			return 0;
		}
	}
	else {
		if peer_ip_filter_out() = 0 then {
			return 0;
		}
	}

	set_originated_routes_as_path();
	tag_originated_routes();
	path_prepend = 0;

	if "global-transit" = rt_scope then {
		path_prepend = 3;
	}
	if "metro-transit" = rt_scope then {
		path_prepend = 3;
	}

	if "bilateral-metro-transit" = rt_scope then {
		if ORIGINATE ~ bgp_community then {
			path_prepend = 2;
		}
	}

	if path_prepend = 1 then {
		bgp_path.prepend(AS_LOCAL);
	}

	if path_prepend = 2 then {
		bgp_path.prepend(AS_LOCAL);
		bgp_path.prepend(AS_LOCAL);
	}

	if path_prepend = 3 then {
		bgp_path.prepend(AS_LOCAL);
		bgp_path.prepend(AS_LOCAL);
		bgp_path.prepend(AS_LOCAL);
	}

	if ("metro-transit" = rt_scope) || ("metro-peering" = rt_scope) then {
		if (ORIGINATE ~ bgp_community) then {
			if (LOCAL_METRO ~ bgp_community) then {
				return 1;
			}
			if METROIZED = 0 then {
				if (AGGREGATE_ROUTE ~ bgp_community) then {
					return 1;
				}
			}
		}
		return 0;
	}

	if "global-transit" = rt_scope || "global-peering" = rt_scope then {
		if (ORIGINATE ~ bgp_community) then {
			if (LOCAL_METRO ~ bgp_community) then {
				return 1;
			}
			if METROIZED = 0 then {
				if (AGGREGATE_ROUTE ~ bgp_community) then {
					return 1;
				}
				if (METRO_ROUTE ~ bgp_community) then {
					return 1;
				}
			}
		}
		return 0;
        }

	if "bilateral-metro-transit" = rt_scope || "metro-transit-customer" = rt_scope then {
		if (ORIGINATE ~ bgp_community) then {
			if (LOCAL_METRO ~ bgp_community) then {
				return 1;
			}
			if METROIZED = 0 then {
				if (AGGREGATE_ROUTE ~ bgp_community) then {
					return 1;
				}
			}
			return 0;
		}

		if (LOCAL_METRO ~ bgp_community) then {
			return 1;
		}
		return 0;
	}

	if "bilateral-global-transit" = rt_scope || "global-transit-customer" = rt_scope then {
		if (ORIGINATE ~ bgp_community) then {
			if METROIZED = 0 then {
				return 1;
			}
			else {
				if (LOCAL_METRO ~ bgp_community) then {
					return 1;
				}
			}
			return 0;
		}

		return 1;
	}
	return 0;
}

function ebgp_in(string rt_scope)
int lpref;
int rt_med;
int metroized;
{
	if peer_ip_filter_in() = 0 then {
		return 0;
	}

	if "global-transit" = rt_scope then {
                lpref = 500; rt_med = 0; metroized = 0;
	}
        if "metro-transit" = rt_scope then {
                lpref = 500; rt_med = 0; metroized = 1;
	}
	if "global-peer" = rt_scope then {
		lpref = 1000; rt_med = 0; metroized = 0;
	}
	if "metro-peer" = rt_scope then {
		lpref = 1000; rt_med = 0; metroized = 1;
	}
	if "px-global-peer" = rt_scope then {
		lpref = 1000; rt_med = 100; metroized = 0;
	}
	if "px-metro-peer" = rt_scope then {
		lpref = 1000; rt_med = 100; metroized = 1;
	}
	if "px-global-backup-peer" = rt_scope then {
		lpref = 1000; rt_med = 500; metroized = 0;
	}
	if "px-metro-backup-peer" = rt_scope then {
		lpref = 1000; rt_med = 500; metroized = 1;
	}
	if "route-server" = rt_scope then {
		lpref = 1000; rt_med = 1000; metroized = 1;
	}

	bgp_local_pref = lpref;
	bgp_med = rt_med;

	bgp_community.add(LOCAL_METRO);
	if METROIZED = 1 then {
		bgp_community.add(METRO_ROUTE);
	}
	else {
		if metroized = 1 then {
			bgp_community.add(METRO_ROUTE);
		}
		else {
			bgp_community.add(GLOBAL_ROUTE);
		}
	}
	return 1;
}

filter global_transit_in {
        if ebgp_in("global-transit") = 1 then
                accept;
        reject;
}

filter metro_transit_in {
        if ebgp_in("metro-transit") = 1 then
                accept;
        reject;
}

filter global_peer_in {
	if ebgp_in("global-peer") = 1 then
		accept;
	reject;
}

filter metro_peer_in {
        if ebgp_in("metro-peer") = 1 then
                accept;
        reject;
}

filter px_global_peer_in {
        if ebgp_in("px-global-peer") = 1 then
                accept;
        reject;
}

filter px_metro_peer_in {
        if ebgp_in("px-metro-peer") = 1 then
                accept;
        reject;
}

filter px_global_backup_peer_in {
        if ebgp_in("px-global-backup-peer") = 1 then
                accept;
        reject;
}

filter px_metro_backup_peer_in {
        if ebgp_in("px-metro-backup-peer") = 1 then
                accept;
        reject;
}

filter route_server_in {
        if ebgp_in("route-server") = 1 then
                accept;
        reject;
}

filter global_transit_out {
	if ebgp_out("global-transit") = 1 then
		accept;
	reject;
}

filter metro_transit_out {
	if ebgp_out("metro-transit") = 1then
                accept;
        reject;
}

filter global_peer_out {
	if ebgp_out("global-peering") = 1 then
                accept;
        reject;
}

filter metro_peer_out {
	if ebgp_out("metro-peering") = 1 then
                accept;
        reject;
}

filter bilateral_metro_transit_out {
	if ebgp_out("bilateral-metro-transit") = 1 then
                accept;
        reject;
}

filter metro_transit_customer_out {
	if ebgp_out("metro-transit-customer") = 1 then
                accept;
        reject;
}

filter bilateral_global_transit_out {
	if ebgp_out("bilateral-metro-transit") = 1 then
                accept;
        reject;
}

filter global_transit_customer_out {
	if ebgp_out("global-transit-customer") = 1 then
                accept;
        reject;
}

filter community_out {
	if DRAINED = 1 then
		reject;
	if net = 0.0.0.0/0 then
		accept;
	reject;
}

